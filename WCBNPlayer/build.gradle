buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}
apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: "libs", include: "*.jar")
    compile "com.android.support:appcompat-v7:22.2.1"
}

if(file("keys_private.gradle").exists()) {
    apply from: 'keys_private.gradle'
}
else {
    logger.warn('DANGER: USING DEFAULT KEYS!!! SEE keys_default.gradle')
    apply from: 'keys_default.gradle'
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
    }

    signingConfigs {
        release_test {
            storeFile keyFileTest
            storePassword storePassTest
            keyAlias keyAliasTest
            keyPassword keyPassTest
        }

        release {
            storeFile keyFileRelease
            storePassword storePassRelease
            keyAlias keyAliasRelease
            keyPassword keyPassRelease
        }
    }

    buildTypes {
        release {
            versionNameSuffix "_rel"+"-"+getVersionName()
        }

        debug {
            versionNameSuffix "_debug"+"-"+getVersionName()
        }
    }

    /* Product flavors: We'll build apps for multiple stations with a single project later on. */
    // Release package name for Google Play
    productFlavors {
        wcbn_deploy {
            applicationId "org.wcbn.player"
            signingConfig signingConfigs.release
        }
        // Alpha/beta testing package name for Google Play
        wcbn_testing {
            applicationId "org.wcbntest.android"
            signingConfig signingConfigs.release_test
        }
    }
}
