buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}
apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: "libs", include: "*.jar")
}

if(file("keys_private.gradle").exists()) {
    apply from: 'keys_private.gradle'
}
else {
    logger.warn('DANGER: USING DEFAULT KEYS!!! SEE keys_default.gradle')
    apply from: 'keys_default.gradle'
}

String getVersionName() {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--dirty'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

String versionName = getVersionName();

android {
    compileSdkVersion 18
    buildToolsVersion "18.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 18
    }

    signingConfigs {
        release_test {
            storeFile keyFileTest
            storePassword storePassTest
            keyAlias keyAliasTest
            keyPassword keyPassTest
        }

        release {
            storeFile keyFileRelease
            storePassword storePassRelease
            keyAlias keyAliasRelease
            keyPassword keyPassRelease
        }
    }

    buildTypes {
        release {
            versionNameSuffix "_rel"+"-"+versionName
        }

        release_alpha.initWith(buildTypes.release)
        release_alpha {
            versionNameSuffix "_alpha"+"-"+versionName
        }

        release_beta.initWith(buildTypes.release)
        release_beta {
            versionNameSuffix "_beta"+"-"+versionName
        }

        debug {
            versionNameSuffix "_debug"+"-"+versionName
        }
    }

    /* Product flavors: We'll build apps for multiple stations with a single project later on. */
    // Release package name for Google Play
    productFlavors {
        wcbn_deploy {
            packageName "org.wcbn.android"
            signingConfig signingConfigs.release
        }
        // Alpha/beta testing package name for Google Play
        wcbn_testing {
            packageName "org.wcbntest.android"
            signingConfig signingConfigs.release_test
        }
    }
}
